// Ez a példa statikus kódellenőrzők használatát mutatja be.

buildscript {
    repositories {
        maven { url 'https://oss.sonatype.org/content/repositories/snapshots/' }
        maven { url 'https://plugins.gradle.org/m2/' }
    }

    dependencies {
        classpath group: 'com.github.spotbugs.snom', name: 'spotbugs-gradle-plugin', version: '6.0.0-rc.2'
        classpath group: 'se.bjurr.violations', name: 'violations-gradle-plugin', version: '1.52.7'
    }
}

apply plugin: 'checkstyle'
apply plugin: 'pmd'
apply plugin: com.github.spotbugs.snom.SpotBugsPlugin
apply plugin: se.bjurr.violations.gradle.plugin.ViolationsGradlePlugin

def configDir = buildscript.sourceFile.parentFile

checkstyle {
    toolVersion = '10.12.4'
    ignoreFailures = true
    configFile = file("${configDir}/checkstyle.xml")
}

spotbugs {
    toolVersion = '4.8.0'
    ignoreFailures = true
    effort = com.github.spotbugs.snom.Effort.MAX
    reportLevel = com.github.spotbugs.snom.Confidence.valueOf('LOW')
    excludeFilter = file("${configDir}/spotbugs-filter.xml")
}

spotbugsMain {
    reports {
        xml.enabled = true
    }
}

pmd {
    toolVersion = '6.55.0'
    ignoreFailures = true
    ruleSetFiles = files("${configDir}/pmd-ruleset.xml")
    ruleSets = []
    rulesMinimumPriority = 5
    try {
        incrementalAnalysis = true
    } catch (def e) {
    }
}

task violations(type: se.bjurr.violations.gradle.plugin.ViolationsTask) {
    maxRuleColumnWidth = 25
    maxMessageColumnWidth = 80

    minSeverity = 'INFO' // INFO, WARN or ERROR
    detailLevel = 'VERBOSE' // PER_FILE_COMPACT, COMPACT or VERBOSE
    maxViolations = 0 // Will fail the build if total number of found violations is higher

    violations = [
            ["FINDBUGS", buildDir.path, ".*/spotbugs/.*\\.xml\$", "SpotBugs"],
            ["PMD", buildDir.path, ".*/pmd/.*\\.xml\$", "PMD"],
            ["CHECKSTYLE", buildDir.path, ".*/checkstyle/.*\\.xml\$", "Checkstyle"]
    ]
}

check.finalizedBy violations
